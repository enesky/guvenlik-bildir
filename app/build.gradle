apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.enesky.guvenlikbildir"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 101
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "google_maps_key", (project.findProperty("GOOGLE_MAPS_API_KEY") ?: "")
    }

    flavorDimensions "default"

    // Load keystore
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        config {
            storeFile file(rootDir.getCanonicalPath() + '/' + keystoreProperties['releaseKeyStore'])
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
            storePassword keystoreProperties['releaseStorePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix = ".dev"
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            buildConfigField "boolean", "LOG_ENABLED", "true"
            resValue "string", "app_name", "Güvenlik Bildir Dev"
        }
        prod {
            buildConfigField "boolean", "LOG_ENABLED", "false"
            resValue "string", "app_name", "Güvenlik Bildir"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures{
        dataBinding = true
        viewBinding = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    //Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    //Work
    implementation "androidx.work:work-runtime-ktx:2.3.4"

    //Paging
    implementation "androidx.paging:paging-runtime:$paging_version"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    //Room
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    // Guava -> Used due to duplicate class error
    implementation 'com.google.guava:guava:27.0.1-android'

    //Firebase
    implementation 'com.google.firebase:firebase-core:17.4.2'
    implementation 'com.google.firebase:firebase-analytics:17.4.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'
    implementation 'com.google.firebase:firebase-auth:19.3.1'
    implementation 'com.google.firebase:firebase-perf:19.0.7'
    implementation 'com.google.firebase:firebase-firestore:21.4.3'
    implementation 'com.google.firebase:firebase-messaging:20.2.0'

    //Google Maps
    implementation "com.google.android.gms:play-services-maps:$google_maps_version"
    implementation "com.google.android.gms:play-services-location:$google_maps_version"

    //Network Libraries
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.squareup.retrofit2:retrofit:2.7.0"
    implementation "com.squareup.retrofit2:converter-gson:2.6.1"
    implementation 'com.squareup.retrofit2:converter-scalars:2.7.0'
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.2"

    //Third Party Libraries
    implementation 'com.bitvale:switcher:1.1.0'
    implementation "com.thefinestartist:finestwebview:1.2.7"
    implementation 'com.github.rubensousa:gravitysnaphelper:2.2.0'
    implementation "com.github.skydoves:transformationlayout:1.0.3"
    implementation 'com.github.quickpermissions:quickpermissions-kotlin:0.4.0'
    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"
    implementation 'com.github.trendyol:medusa:0.8.0'
    implementation 'com.reddit:indicator-fast-scroll:1.2.1'
    implementation 'com.redmadrobot:input-mask-android:5.0.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    implementation 'me.samlss:broccoli:1.0.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.github.tajchert:WaitingDots:0.6.1'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
